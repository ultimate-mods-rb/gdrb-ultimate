(Object
   (types
      (track_callback
         (init
            ($player)
            {set [player] $player}
            {[player] add_sink $this}
            {set [track] {get_dir_from_player $player}}
            {set [streak_meter] {[track] get streak_meter}}
            {if $fcring
               {with [streak_meter]
                  {unless {exists fake.trig}
                     {{new EventTrigger show_ring.trig} copy star_deploy.trig 0}
                     {{new EventTrigger hide_ring.trig} copy star_deploy_stop.trig 0}

                     {new EventTrigger fake.trig}
                     {star_deploy.trig copy fake.trig 0}
                     {star_deploy_stop.trig copy fake.trig 0}
                  }
                  {show_ring.trig trigger}
               }
            }
         )
         (hit
            {++ [gems_hit]}
            {++ [gems_combo]}
            {if {&& {! {|| $legacybass {== {[player] instrument} bass}}} {> [gems_combo] 29} {! [groove_active]}}
               {set [groove_active] TRUE}
               {{[track] find BassSuperStreak_ON.trig} trigger}
            }
         )
         (miss
            {unless
               {||
                  {[player] current_streak}
                  {== {+ [gems_hit] [gems_pass]} 0}
               }
               {++ [gems_miss]}
               {set [gems_combo] 0}
               {$this on_miss}
            }
         )
         (pass
            {unless {[player] current_streak}
               {++ [gems_pass]}
               {set [gems_combo] 0}
               {$this on_miss}
            }
         )
         (phrase_rating
            ($rating)
            {if_else {== $rating 4}
               {do
                  {++ [gems_hit]}
                  {++ [gems_combo]}
               }
               {do
                  {++ [gems_miss]}
                  {set [gems_combo] 0}
                  {$this on_miss}
               }
            }
         )
         (on_miss
            {if $fcring
               {{[streak_meter] find hide_ring.trig} trigger}
            }
            {if [groove_active]
               {set [groove_active] FALSE}
               {{[track] find BassSuperStreak_OFF.trig} trigger}
            }
         )
         (gems_hit 0)
         (gems_combo 0)
         (gems_miss 0)
         (gems_pass 0)
         (groove_active FALSE)
         (player 0)
         (track 0)
         (streak_meter 0)
      )
   )
)
(BandList
   (types
      (overshell (resource_file "resource/list_overshell.milo"))
   )
)
#define ULTI_FONTS
(
   ('BentonSansComp--Ulti(48)'
      (resource_file "resource/bentonsanscomp-black(48).milo")
   )
)
(UILabel (types ULTI_FONTS))
(BandLabel (types ULTI_FONTS))